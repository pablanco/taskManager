// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    ANDROID_COMPILE_SDK_VERSION = 21
    ANDROID_BUILD_TOOLS_VERSION = '22.0.1'
    ANDROID_MIN_SDK_VERSION = 9
    ANDROID_TARGET_SDK_VERSION = 21
    preDexLibs = !project.hasProperty('disablePreDex')
    println('Pre-dex ' + (preDexLibs ? 'enabled' : 'disabled') + '.')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

def androidLibraryProjects() {
    def libraryNames = ['AChartEngine', 'AltBeacon', 'BaiduMaps', 'ChartsLib', 'Facebook',
                        'GaodeMaps', 'InAppBillingLib', 'SimpleKMLLib', 'spinner-library', 'TwitterApi',
                        'ViewPagersLib', 'WheelsLib', 'FlexibleClient']
    return subprojects.findAll {
        project -> libraryNames.contains(project.name)
    }
}

buildscript {
    repositories {
        maven {
            url gradle.ext.m2RepositoryUrl
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

rootProject.afterEvaluate { project ->
    println 'Configuring projects:'
}

subprojects {
    repositories {
        maven {
            url gradle.ext.m2RepositoryUrl
        }
    }
    afterEvaluate { project ->
        println '  ' + project.name + '... done.'
    }
}

configure(androidLibraryProjects()) {
    apply plugin: 'com.android.library'
    android {
        compileSdkVersion ANDROID_COMPILE_SDK_VERSION
        buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion ANDROID_MIN_SDK_VERSION
            targetSdkVersion ANDROID_TARGET_SDK_VERSION
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        dexOptions {
            preDexLibraries = rootProject.ext.preDexLibs
            jumboMode = true
        }

        lintOptions {
            abortOnError false
        }

        sourceSets {
            main {
                manifest.srcFile 'AndroidManifest.xml'
                java.srcDirs = ['src']
                resources.srcDirs = ['src']
                aidl.srcDirs = ['src']
                renderscript.srcDirs = ['src']
                res.srcDirs = ['res']
                assets.srcDirs = ['assets']
            }

            test {
                java.srcDirs = ['test/java']
            }

            // Move the tests to tests/java, tests/res, etc...
            instrumentTest.setRoot('tests')

            // Move the build types to build-types/<type>
            debug.setRoot('build-types/debug')
            release.setRoot('build-types/release')
        }
    }
}
